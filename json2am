#!/usr/bin/perl
use strict;
use warnings;
use Image::Magick;
use Data::Dumper;
$Data::Dumper::Indent = 1;

sub pal {
    my ($image) = @_;
    my %colors;
    my @pixels = $image->GetPixels(width=>$image->Get("width"), height=>$image->Get("height"));
    $_ >>= 8 for @pixels;
    $colors{join ",", splice @pixels, 0, 3}++ while @pixels;
    my @colors = map [split /,/], keys %colors;
    @colors == 16 or die "ERROR: image must have exactly 16 colors but has ",
        scalar @colors, ": ", (Dumper \@colors), "\n";
    my @reds = grep { $_->[0] > $_->[1] && $_->[0] > $_->[2] } @colors;
    my @greens = grep { $_->[1] > $_->[0] && $_->[0] > $_->[2] } @colors;
    my @blues = grep { $_->[2] > $_->[1] && $_->[2] > $_->[0] } @colors;
    my @greys = grep { $_->[0] == $_->[1] && $_->[0] == $_->[2] } @colors;
    @reds = sort { $a->[0] <=> $b->[0] } @reds;
    @greens = sort { $a->[1] <=> $b->[1] } @greens;
    @blues = sort { $a->[2] <=> $b->[2] } @blues;
    @greys = sort { $a->[0] <=> $b->[0] } @greys;
    my %pal;
    for ([\@reds, 1], [\@greens, 3], [\@blues, 2], [\@greys, 0]) {
        my ($hue, $val) = @$_;
        $pal{join ",", @{$hue->[$_]}} = [$val, $_] for 0 .. 3;
    }
    $pal{$_} = $pal{$_}[0] << 2 | $pal{$_}[1] for keys %pal;
    return \%pal;
}

sub parsejson {
    my ($file) = @_;
    open my $fh, $file or die "ERROR: Cannot open $file: $!\n";
    my $text = join "", <$fh>;
    $text =~ s/:/=>/g;
    $text =~ s/=>true/=>1/g;
    $text =~ s/=>false/=>0/g;
    my $struct = eval $text or die $@;
    return $struct;
}

sub readimage {
    my ($file) = @_;
    my $image = Image::Magick->new;
    my $status = $image->Read($file);
    warn "WARNING: $status" if $status;
    return $image;
}

sub dump_tile {
    my ($tileset, $image, $tile) = @_;
    my $imagewidth = $tileset->{imagewidth} or die;
    my $imageheight = $tileset->{imageheight} or die;
    my $tilewidth = $tileset->{tilewidth} or die;
    my $tileheight = $tileset->{tileheight} or die;
    my $dy = int($imagewidth / $tilewidth);
    my $xo = $tilewidth * (($tile - 1) % $dy);
    my $yo = $tileheight * int(($tile - 1) / $dy);
    warn "TILE: $tile: DY: $dy XO: $xo YO: $yo\n";
    my @pixels;
    for (my $x = $xo; $x < $xo + $tilewidth; $x += 4) {
        push @pixels, $image->GetPixels(x => $x, y => $yo, width => 4, height => $tileheight);
    }
    return @pixels;
}

sub chline {
    my (@bmp) = @_;
    my $hue = ($bmp[0]<<4&0xC0)|($bmp[1]<<2&0x30)|($bmp[2]<<0&0xC)|($bmp[3]>>2&0x3);
    my $lum = ($bmp[0]<<6&0xC0)|($bmp[1]<<4&0x30)|($bmp[2]<<2&0xC)|($bmp[3]<<0&0x3);
    return ($hue, $lum);
}

sub dump_chset {
    my ($map, $palimage, $tiles) = @_;
    my $tileset = $map->{tilesets}[0];
    my $image = readimage($tileset->{image});
    my $pal = pal($palimage);
    my @pixels;
    push @pixels, dump_tile($tileset, $image, $_) for @$tiles;
    $_ >>= 8 for @pixels;
    my @bmp;
    push @bmp, $pal->{join ",", splice @pixels, 0, 3} || 0 while @pixels;
    my @chset;
    push @chset, chline(splice @bmp, 0, 4) while @bmp;
    if (@chset > 8*128) {
        warn "WARNING: Truncating charset to 128 chars. ",
            "Set has ", (@chset/8), "chars.\n";
        splice @chset, 0, 8*128;
    }
    my $ch = 0;
    print "  dta ", (join ",", splice @chset, 0, 8), " ; ", $ch++, "\n" while @chset;
}

sub dump_map {
    my ($map) = @_;
    my @copy = map $_-1, @$map;
    print "  org map\n";
    print "  dta ", (join ",", splice @copy, 0, 32), "\n" while @copy;
}

sub main {
    my $map = parsejson($ARGV[0]);
    my $palimage = readimage($ARGV[1]);
    my $layer = $map->{layers}[0];
    my $data = $layer->{data} or die;
    my %counts;
    $counts{$_}++ for @$data;
    my @tiles = sort { $counts{$b} <=> $counts{$a} } keys %counts;
    my %raw2tile = map { $tiles[$_] => $_+1 } 0 .. $#tiles;
    my @map = map { $raw2tile{$_} || 0 } @$data;
    print "  org buffer\n";
    print "  ; Dest Start Address, Dest End Address, Bank\n";
    print "  dta a(chset),a(chset+\$1000),0\n";
    for my $set (0 .. 3) {
        dump_chset($map, $palimage, [map $_+$set*8, @tiles]);
    }
    print "  ini relocate\n";
    dump_map(\@map);
}

main();
